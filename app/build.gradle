apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'

def storeKeyPropertiesFile = rootProject.file("storeKey.properties")
def storeKeyProperties = new Properties()
storeKeyProperties.load(new FileInputStream(storeKeyPropertiesFile))

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    namespace "com.nasa.nasa_app"
    compileSdk 33

    signingConfigs {
        release {
            storeFile file(storeKeyProperties['FILE_KEY'])
            storePassword storeKeyProperties['STORE_KEY']
            keyAlias storeKeyProperties['ALIAS_KEY']
            keyPassword storeKeyProperties['STORE_KEY']
        }
    }

    defaultConfig {
        applicationId "com.nasa.nasaApp"
        minSdk 26
        targetSdk 33
        versionCode 3
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField "String", "PRIVATE_KEY", apikeyProperties['PRIVATE_KEY']
        buildConfigField "String", "BASE_URL", '"https://api.nasa.gov/"'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    buildTypes {
        debug{
            applicationIdSuffix '.debug'
            debuggable = true
        }
        staging{
            initWith debug
            minifyEnabled true
            applicationIdSuffix '.staging'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'proguard-rules-staging.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    buildFeatures {
        compose true
    }
    lint {
        abortOnError false
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    implementation project(":domain")
    testImplementation project(":testing")

    implementation libs.bundles.app.android

    //Compose
    implementation libs.bundles.app.compose

    //Hilt
    implementation libs.hilt.android
    kapt libs.hilt.android.compiler

    //Firebase
    implementation libs.firebase.bom
//    implementation libs.firebase.analytics

    implementation libs.moshi

    //Test
    implementation libs.bundles.app.test
}